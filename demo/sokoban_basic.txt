title 2 Player Sokoban
author Joseph Mansfield
homepage www.puzzlescript.net

========
OBJECTS
========

Background
lightgreen green
11111
01111
11101
11111
10111

Target
darkblue
.....
.000.
.0.0.
.000.
.....

Wall
brown darkbrown
00010
11111
01000
11111
00010

Player1
black orange white blue
.000.
.111.
22222
.333.
.3.3.

Player2
white brown red pink
.000.
.111.
22222
.333.
.3.3.

Crate
orange
00000
0...0
0...0
0...0
00000

=======
LEGEND
=======

. = Background
# = Wall
P = Player1
Q = Player2
* = Crate
@ = Crate and Target
O = Target

Player = Player1 or Player2
Pushable = Crate or Player

=======
SOUNDS
=======

Crate move 36772507

================
COLLISIONLAYERS
================

Background
Target
Player1, Player2, Wall, Crate

======
RULES
======

[ > Pushable | Pushable ] -> [ > Pushable | > Pushable ] 

==============
WINCONDITIONS
==============

all Target on Crate

=======
LEVELS
=======

#########
#......O#
#.P...Q.#
#.......#
#...*...#
#.......#
#########

